@PACKAGE_INIT@

# Set and Check must go first else macro might be redefined by find_dependency ...
set_and_check(ALIEN_MODULE_PATH "@PACKAGE_ALIEN_MODULE_PATH@")
set_and_check(ALIEN_TARGET_FILE "${ALIEN_MODULE_PATH}/@ALIEN_EXPORT_TARGET@.cmake")


include(CMakeFindDependencyMacro)

find_dependency(Arccon)
# Add FindPackage from Arccon
list(APPEND CMAKE_MODULE_PATH ${ARCCON_MODULE_PATH})
find_dependency(Arccore)

find_dependency(BLAS)
find_dependency(Boost)
find_dependency(Eigen3)

set(ALIEN_USE_CUDA @ALIEN_USE_CUDA@)
set(ALIEN_USE_HIP @ALIEN_USE_HIP@)
set(ALIEN_USE_SYCL @ALIEN_USE_SYCL@)
set(ALIEN_USE_HIPSYCL @ALIEN_USE_HIPSYCL@)
set(ALIEN_USE_ACPPSYCL @ALIEN_USE_ACPPSYCL@)
set(ALIEN_USE_INTELDPCPP @ALIEN_USE_INTELDPCPP@)
set(ALIEN_USE_INTELSYCL @ALIEN_USE_INTELSYCL@)

if(ALIEN_USE_CUDA)
    enable_language(CUDA)
    find_package(CUDAToolkit REQUIRED)
endif()

if(ALIEN_USE_SYCL)
  if(ALIEN_USE_HIPSYCL)
    find_package(hipSYCL CONFIG REQUIRED)
  endif()
  if (ALIEN_USE_ACPPSYCL)
    find_package(AdaptiveCpp REQUIRED)
    find_package(Boost COMPONENTS fiber context REQUIRED)
  endif()
  if(ALIEN_USE_INTELDPCPP)
    find_package(IntelDPCPP CONFIG REQUIRED)
  endif()
  if(ALIEN_USE_INTELSYCL)
    find_package(IntelSYCL REQUIRED)
  endif()
endif()

# Remove FindPackage Arccon paths
list(REMOVE_AT CMAKE_MODULE_PATH -1)

# set_and_check already done.
include(${ALIEN_TARGET_FILE})
list(APPEND CMAKE_MODULE_PATH ${ALIEN_MODULE_PATH})

